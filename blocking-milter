#!/usr/bin/env perl

use strict;
use warnings;

use autodie qw(:all);
use Sendmail::PMilter qw(:all);
use Sys::Syslog qw(:standard :macros);

# Define the phrases to block
my @block_phrases = (
	'SEO audit',
	'optimize your website',
	'increase your traffic',
	'boost your rankings',
	'Free Website Audit',
	'audit report of your website',
);

# Create a new milter
my $milter = Sendmail::PMilter->new();
$milter->auto_setconn('blocking-milter');

# Define the callback for filtering messages
$milter->register('blocking-milter',
	{
		envfrom => sub {
			my ($ctx, $from) = @_;
			return SMFIS_CONTINUE; # Continue processing
		}, header => sub {
			my ($ctx, $header, $value) = @_;

			# Check headers for spam phrases
			foreach my $phrase (@block_phrases) {
				if ($value =~ /\b\Q$phrase\E\b/i) {
					$ctx->setreply(554, '5.7.1', 'Rejecting your spam');
					syslog(LOG_WARNING, 'Message rejected: spam detected');
					return SMFIS_REJECT; # Reject the message
				}
			}
			return SMFIS_CONTINUE; # Continue processing
		}, body => sub {
			my ($ctx, $body) = @_;

			# Check body for spam phrases (doesn't work if the phrase spans two blocks)
			foreach my $phrase (@block_phrases) {
				if($body =~ /\b\Q$phrase\E\b/i) {
					$ctx->setreply(554, '5.7.1', 'Rejecting your spam');
					syslog(LOG_WARNING, 'Message rejected: spam detected');
					return SMFIS_REJECT; # Reject the message
				}
			}
			return SMFIS_CONTINUE; # Continue processing
		}, eom => sub {
			my $ctx = shift;
			return SMFIS_ACCEPT;	# If we've got this far, all is good
		}, abort => sub {
			my $ctx = shift;
			return SMFIS_CONTINUE; # Handle aborts gracefully
		},
	}
);

# Run the milter
# Sendmail::Milter::main('blocking-milter', '/var/run/blocking-milter.sock')
#	or die "Failed to start milter: $!";

$milter->main();
